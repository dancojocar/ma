licenses(["notice"])  # Apache 2.0

load("//:common_defs.bzl", "minSdkVersion", "targetSdkVersion")
load("@rules_jvm_external//:defs.bzl", "artifact")

android_library(
    name = "EspressoDeviceSampleLib",
    srcs = glob(["app/src/main/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.EspressoDeviceSample",
    manifest = "app/src/main/AndroidManifest.xml",
    resource_files = glob(["app/src/main/res/**/*"]),
)

android_binary(
    name = "EspressoDeviceSample",
    custom_package = "com.example.android.testing.espresso.EspressoDeviceSample",
    manifest = "app/src/main/AppManifest.xml",
    manifest_values = {
        "minSdkVersion": minSdkVersion,
        "targetSdkVersion": targetSdkVersion,
    },
    deps = [":EspressoDeviceSampleLib"],
)

android_library(
    name = "EspressoDeviceSampleTestLib",
    srcs = glob(["app/src/androidTest/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.EspressoDeviceSample.test",
    deps = [
        ":EspressoDeviceSampleLib",
        "//:test_deps",
    ],
)

android_binary(
    name = "EspressoDeviceSampleTest",
    custom_package = "com.example.android.testing.espresso.EspressoDeviceSample.test",
    instruments = ":EspressoDeviceSample",
    manifest = "app/src/androidTest/AndroidManifest.xml",
    manifest_values = {
        "minSdkVersion": minSdkVersion,
        "targetSdkVersion": targetSdkVersion,
    },
    deps = [":EspressoDeviceSampleTestLib"],
)

API_LEVELS = [
    "19_x86",
    "21_x86",
    "22_x86",
    "23_x86",
]

[android_instrumentation_test(
    name = "EspressoDeviceSampleInstrumentationTest_%s" % API_LEVEL,
    target_device = "@android_test_support//tools/android/emulated_devices/generic_phone:android_%s_qemu2" % API_LEVEL,
    test_app = ":EspressoDeviceSampleTest",
) for API_LEVEL in API_LEVELS]
